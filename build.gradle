buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${minecraft_version}-${mod_version}"
group = 'bedrockbreaker.graduatedcylinders'
archivesBaseName = 'GraduatedCylinders'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
}

repositories {
    maven { url "https://cursemaven.com" }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    compileOnly fg.deobf("curse.maven:mekanismapi-268560:2835176") // Mekanism's real maven doesn't have files for 1.12
    //runtimeOnly fg.deobf("curse.maven:mekanism-268560:2835175")
}

jar {
    manifest {
        attributes([
            "Specification-Title": "graduatedcylinders",
            "Specification-Vendor": "graduatedcylinders",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"graduatedcylinders",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task apiJar(type: Jar) {
    classifier = "api"
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)
    include "bedrockbreaker/graduatedcylinders/api/**"
}

artifacts {
    archives apiJar
}

jar.finalizedBy('reobfJar') 

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}